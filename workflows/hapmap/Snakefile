#configfile: "workflows/pedsim/config.yaml"

CHROMOSOMES     = [str(i) for i in range(1, 23)]

CHIP_DATA_LINK = 'ftp://ftp.1000genomes.ebi.ac.uk/vol1/ftp/release/20130502/supporting/hd_genotype_chip/ALL.chip.omni_broad_sanger_combined.20140818.snps.genotypes.vcf.gz'
CHIP_INDEX_LINK = 'ftp://ftp.1000genomes.ebi.ac.uk/vol1/ftp/release/20130502/supporting/hd_genotype_chip/ALL.chip.omni_broad_sanger_combined.20140818.snps.genotypes.vcf.gz.tbi'

rule all:
    input:
        "results/relatives.tsv",
        "results/accuracy.png"


rule recode_vcf:
    input:
        ped='/media/ref/hapmap/ceu.ped',
        mp='/media/ref/hapmap/ceu.map'
    output:
        vcf='vcf/merged_recoded.vcf.gz'
    params:
        out='vcf/merged_recoded'
    conda: "../../envs/plink.yaml"
    shell: "plink --ped {input.ped} --map {input.mp} --snps-only just-acgt --output-chr M --not-chr XY,MT --export vcf bgz --out {params.out}"


rule postprocess:
    input:
        vcf=rules.recode_vcf.output['vcf'],
        fam='/media/ref/hapmap/relations.txt'
    output:
        kin='kinship/reheaded_data.kinship',
        vcf='vcf/merged_sorted.vcf.gz',
        fam='kinship/reheaded_data.fam'
    conda:
        "envs/hapmap.yaml"
    script:
        "scripts/hapmap.py"

rule liftover:
    input:
        vcf=rules.postprocess.output['vcf'],
        chain='/media/ref/hg18ToHg19.over.chain.gz',
        ref='/media/ref/human_g1k_v37.fasta'
    output:
        vcf="vcf/merged_mapped_sorted.vcf.gz"

    #conda: "envs/crossmap.yaml"
    singularity: "docker://alexgenx/picard:latest"
    shell:
        """
            java -Xmx16G -jar /picard/picard.jar LiftoverVcf MAX_RECORDS_IN_RAM=100000 I={input.vcf} O={output.vcf} CHAIN={input.chain} REJECT=vcf/rejected.vcf R={input.ref}
        """

include: "../../rules/imputation.smk"

include: "../../rules/relatives.smk"

rule evaluate_accuracy:
    input:
        rel=rules.merge_king_ersa.output[0],
        fam=rules.postprocess.output['fam']
    output:
        'results/accuracy.png'
    script:
        '../../scripts/evaluate.py'


