from os.path import join
import os
import random
import string

REF_DIR            = config["ref_dir"]
NUM_RUNS           = list(range(int(config["num_runs"])))
REF_VCF            = join(REF_DIR, config["reference"]["vcfRef"]["file"])
AFFYMETRIX_CHIP    = join(REF_DIR, config["reference"]["affymetrix_chip"]["file"])
PEDSIM_MAP         = join(REF_DIR, config["reference"]["pedsim_map"]["file"])
CHROMOSOMES     = [str(i) for i in range(1, 23)]

names = []
def generate_code():
    length = 6
    while True:
        code = ''.join(random.choices(string.ascii_uppercase, k=length))
        if not(code in names):
            break
    names.append(code)
    return code

def prepare_folders():
    for i in NUM_RUNS:
        if not os.path.exists(f"gen{i}"):
            os.makedirs(f"gen{i}")
        os.system(f"cp -R params gen{i}/params")
        with open(f"{config['sim_params_file']}", "r") as f:
            lines = f.readlines()
            first = lines[0].split(" ")
            name = generate_code()
            first[1] = name
            lines[0] = " ".join(first)

        with open(f"gen{i}/params/relatives_big.def", "w") as f:
            f.write("\n".join(lines))



#CHIP_DATA_LINK = 'ftp://ftp.1000genomes.ebi.ac.uk/vol1/ftp/release/20130502/supporting/hd_genotype_chip/ALL.chip.omni_broad_sanger_combined.20140818.snps.genotypes.vcf.gz'
#CHIP_INDEX_LINK = 'ftp://ftp.1000genomes.ebi.ac.uk/vol1/ftp/release/20130502/supporting/hd_genotype_chip/ALL.chip.omni_broad_sanger_combined.20140818.snps.genotypes.vcf.gz.tbi'

rule all:
    input:
        "generated.vcf.gz",
        "generated.vcf.gz.csi"


rule intersect:
    input:
        hd_genotype_chip=AFFYMETRIX_CHIP,
        vcfRef=REF_VCF
    output:
        temp("pedsim/phased/chr{chrom}.phased.vcf.gz")
    conda: "../../envs/bcftools.yaml"
    shell:
        """
            bcftools isec -n=2 -w1 -r {wildcards.chrom} -O z -o {output} {input.vcfRef} {input.hd_genotype_chip}
        """


rule merge_background:
    input:
        data=expand("pedsim/phased/chr{chrom}.phased.vcf.gz", chrom=CHROMOSOMES)
    output:
        temp("pedsim/phased/background.vcf.gz")
    params:
        list="pedsim/phased/phased.merge.list"
    conda:
        "../../envs/bcftools.yaml"
    shell:
        """
            # for now just skip empty files
            true > {params.list} && \
            for i in {input.data}; do
                if [ -s $i ]
                then
                    echo $i >> {params.list}
                else
                    continue
                fi
            done
            bcftools concat -f {params.list} -O z -o {output}
        """


rule simulate:
    input:
        bg=rules.merge_background.output,
        _map=PEDSIM_MAP,
        intf='params/nu_p_campbell.tsv'
    output:
        data = temp('gen{num_runs}/data{num_runs}.vcf.gz')
    conda:
        "../../envs/ped-sim.yaml"
    shell:
        """
            pedsim -d gen{wildcards.num_runs}/params/relatives_big.def -m {input._map} -i {input.bg} -o gen{wildcards.num_runs}/data{wildcards.num_runs} --intf {input.intf} --fam
        """

rule convert:
    input:
        "gen{num_runs}/data{num_runs}.vcf.gz"
    output:
        temp("gen{num_runs}/data4merge{num_runs}.vcf.gz")
    conda:
        "../../envs/bcftools.yaml"
    shell:
        """
            bcftools convert gen{wildcards.num_runs}/data{wildcards.num_runs}.vcf.gz -O z -o  gen{wildcards.num_runs}/data4merge{wildcards.num_runs}.vcf.gz
        """

rule index:
    input:
        "gen{num_runs}/data4merge{num_runs}.vcf.gz"
    output:
        temp("gen{num_runs}/data4merge{num_runs}.vcf.gz.csi")
    conda: "../../envs/bcftools.yaml"
    shell:
        """
            bcftools index -f gen{wildcards.num_runs}/data4merge{wildcards.num_runs}.vcf.gz
        """

rule merge:
    input:
        expand("gen{num_runs}/data4merge{num_runs}.vcf.gz.csi", num_runs = NUM_RUNS),
        expand("gen{num_runs}/data4merge{num_runs}.vcf.gz", num_runs = NUM_RUNS)
    output:
        "generated.vcf.gz",
        "generated.vcf.gz.csi"
    params:
        files = expand(" gen{num_runs}/data4merge{num_runs}.vcf.gz ", num_runs = NUM_RUNS)
    conda: "../../envs/bcftools.yaml"
    shell:
        """   
            bcftools merge --merge id {params.files} -O z -o generated.vcf.gz
            bcftools index -f generated.vcf.gz
        """
